pipeline WolrdBankPipeline {

    // Defining a pipeline with three pipes
    // First pipe or Composite block to get data
    WolrdBankExtractor
        -> WolrdBankXLXSInterpreter
        -> WolrdBankSheetPicker
        -> WolrdBankCellRangeSelector
        -> WorldBankNameHeaderWriter;

    // Second pipe for “bondIssuance” table
    WorldBankNameHeaderWriter
        -> bondIssuanceTableInterpreter
        -> bondIssuanceLoader;

    // Third pipe for gdpPerCapita table
    WorldBankNameHeaderWriter
        -> gdpPerCapitaTableInterpreter
        -> gdpPerCapitaLoader;


    // Defining the Composite block
    block WolrdBankExtractor oftype HttpExtractor {
        url: "https://thedocs.worldBank.org/en/doc/7d852628d96b9411d43e5d36d5dff941-0050062022/original/Graphs-Chapter-5-02082022.xlsx";
    }

    block WolrdBankXLXSInterpreter oftype XLSXInterpreter { }

    block WolrdBankSheetPicker oftype SheetPicker {
        sheetName: "Figure S5.1.2";
    }

    block WolrdBankCellRangeSelector oftype CellRangeSelector {
        select: range P2:S45;
    }

    block WorldBankNameHeaderWriter oftype CellWriter {
    at: range A1:D1;
    write: [
      "Country Code",
      "Economy",
      "GDP per Capita",
      "Bond Issuance Share",
    ];
    }

    // Implementing logic to get the “bondIssuance” table by using output of composite block
    block bondIssuanceTableInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "Country Code" oftype CountryCodeAlpha3,
            "Bond Issuance Share" oftype BondValueRangeType
        ];
    }

    block bondIssuanceLoader oftype SQLiteLoader {
        table: "bondIssuance";
        file: "./country-stats.sqlite";
    }

    // Implementing logic to get the “gdpPerCapita” table by using output of composite block
    block gdpPerCapitaTableInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "Country Code" oftype CountryCodeAlpha3,
            "GDP per Capita" oftype PositiveDecimalGDPType
        ];
    }

    block gdpPerCapitaLoader oftype SQLiteLoader {
        table: "gdpPerCapita";
        file: "./country-stats.sqlite";
    }
} 
   
constraint Range_0_1 oftype RangeConstraint {
lowerBound: 0;
upperBound: 1;
}

valuetype BondValueRangeType oftype decimal {
constraints: [Range_0_1];
}

constraint PositiveDecimal oftype RangeConstraint {
lowerBound: 0;
lowerBoundInclusive: false;
}

valuetype PositiveDecimalGDPType oftype decimal {
constraints: [PositiveDecimal];
}